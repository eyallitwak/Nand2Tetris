// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Handling x value
    Mux16(a=x , b=false , sel=zx , out=modifiedx );
    Not16(in=modifiedx , out=negatedx );
    Mux16(a=modifiedx , b=negatedx , sel=nx , out=finalx );

    //// Handling y value
    Mux16(a=y , b=false , sel=zy , out=modifiedy );
    Not16(in=modifiedy , out=negatedy );
    Mux16(a=modifiedy , b=negatedy , sel=ny , out=finaly );

    //// Calculating possible outputs according to f, no
    Add16(a =finalx , b =finaly , out =xAdd16y );
    And16(a=finalx , b=finaly , out=xAnd16y );
    Mux16(a=xAnd16y , b=xAdd16y , sel=f , out=modifiedout );
    Not16(in=modifiedout , out=negatedout );
    
    ////getting ng, splitting output to get zr
    Mux16(a=modifiedout , b=negatedout , sel=no , out[15]=ng , out=output , out[0..7]=halfout1 , out[8..15]=halfout2);
    
    ////dummy gate to get out
    And16(a=output , b=true , out=out );
    
    ////getting zr
    Or8Way(in=halfout1 , out=zrhalf1 );
    Or8Way(in=halfout2 , out=zrhalf2 );
    Or(a=zrhalf1 , b=zrhalf2 , out=negatedzr );
    Not(in=negatedzr , out=zr );
}